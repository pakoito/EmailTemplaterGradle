/*
 * This file contans settings for every build setting a common build might want
 * to set in a standard build.  Not every possible crazy setting, just the ones
 * a well behaved build might want to set.
 *
 * For the bundleArguments section I only set values that are not already set
 * by the convention or sniffed out from the other configurations in the
 * application.
 */

apply from: 'javafx.plugin'
apply plugin: 'idea'
apply plugin: 'maven'

// e(fx)clipse integration, not part of the Gradle JavaFX Plugin proper
// just here for integration testing
apply plugin: 'eclipse'
eclipse {
    project {
        natures 'org.eclipse.jdt.core.javanature', 'org.eclipse.xtext.ui.shared.xtextNature'
        buildCommand 'org.eclipse.jdt.core.javabuilder'
        buildCommand 'org.eclipse.xtext.ui.shared.xtextBuilder'
    }

    classpath {
        containers 'at.bestsolution.efxclipse.tooling.jdt.core.JAVAFX_CONTAINER'
    }
}


repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jfxtras:jfxtras-labs:2.2-r4'
    compile 'org.controlsfx:controlsfx:8.20.8'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.2.+'
    compile 'com.fasterxml.jackson.core:jackson-core:2.2.+'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.2.+'
    compile 'javax.mail:mail:1.4.7'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

group = 'com.bitbucket.shemnon.javafxplugin.samples'
version = '8.1.2-sample'

javafx {

    profiles {
        // not every possible platform override, but ones that have a known impact
        windows {
            id = 'c533f663-1efd-489f-b910-4c7ec20c7fd0'
            category = 'JavaFX Demos'
            javaRuntime = 'C:/Program Files (x86)/Java/jdk1.8.0_20'

            bundleArguments = [
                    // for EXE and MSI bundles
                    'win.menuGroup'         : 'Gradle Samples',     //If menuHint is set, then this is the menu group to install the application into.

                    // fir MSI bundles, they have stricter version numbering rules
                    'win.msi.productVersion': '8.1.2'
            ]
        }

        macosx {
            category = 'public.app-category.developer-tools'
            javaRuntime = '/Library/Java/JavaVirtualMachines/jdk1.8.0_25.jdk/'

            bundleArguments = [
                    // for mac apps
                    'mac.category'                : 'public.app-category.developer-tools',
                    'mac.CFBundleIdentifier'      : 'com.shemnon.gradle-plugin.FullyExpressed',
                    'mac.CFBundleName'            : 'Fully Expressed',
                    'mac.signing-key-user-name'   : 'Danno Ferrin',
                    //'mac.signing-key-developer-id-app' : 'Developer ID Application: Danno Ferrin' // usually redundant with mac.signing-key-user-name
                    'mac.bundle-id-signing-prefix': 'com.shemnon.gradle-plugin.FE.',

                    // for PKG bundles
                    //'mac.signing-key-developer-id-installer' : 'Developer ID Installer: Danno Ferrin', // usually redundant with mac.signing-key-user-name

                    // for Mac App Store Ready bundles
                    'mac.app-store-entitlements'  : 'src/deploy/package/NetworkEntitlements.entitlements',
                    //'mac.signing-key-app' : '3rd Party Mac Developer Applicaiton: Danno Ferrin', // usually redundant with mac.signing-key-user-name
                    //'mac.signing-key-pkg' : '3rd Party Mac Developer Installer: Danno Ferrin', // usually redundant with mac.signing-key-user-name

            ]
        }

        linux {
            // linux doesn't care about id, it goes un-used at the moment
            category = 'Development'
            // good luck finding a standard location for the JDK
            //javaRuntime = '/usr/lib/jvm/java-7-oracle'

            bundleArguments = [
                    // for DEB bundles
                    'email': 'support@example.com', // This is the email used for the deb maintainer field.
            ]
        }
    }

    debugKey {
        alias = 'buggyKey'
        keyPass = '123456'
        keyStore = file('veryinsecure.jks')
        storePass = '123456'
    }
    // don't get too excited, the release key is overridden by gradle.properties
    releaseKey {
        alias = 'imaginary'
        keyPass = 'bad'
        keyStore = file('nonexistant.jks')
        storePass = 'bad'
    }
    signingMode 'release'

    appID 'FullyExpressed'
    appName 'Fully Expressed Build File'
    mainClass 'com.coolchick.translatortemplater.Main'

    jvmArgs = ['-XX:+AggressiveOpts', '-XX:CompileThreshold=1']
    systemProperties = ['prism.disableRegionCaching': 'true']
    arguments = ['1AC', '1NC', '2AC', '2NC', '1NR', '1AR', '2NR', '2AR']

    embedLauncher = true // caution: class-path not set and is overwritten if set to false

    // applet and webstart stuff
    width = 800
    height = 600
    embedJNLP = false

    // deplpy/info attributes
    category = 'demo'
    copyright = 'Copyright (c) 2012, 2014 Danno Ferrin'
    description = '''If it can and ever should be configured, it is configured in this build.
But really, you should live with the conventions when appropriate.'''
    licenseType = '3 clause BSD'
    vendor = 'Me!'

    // deploy/preferences attributes
    installSystemWide = true
    menu = true
    shortcut = true

    codebase = 'http://example.com/bogus/JNLP/Codebase'

    // app icons
    icons {
        shortcut = ['shortcut-16.png', 'shortcut-32.png', 'shortcut-128.png', 'shortcut-256.png', 'shortcut-16@2x.png', 'shortcut-32@2x.png', 'shortcut-128@2x.png']
        volume = 'javafx-icon.png'
        setup = 'javafx-icon.png'
    }

    // jnlp icons, legacy
    icon {
        href = 'src/main/resources/javafx-icon.png'
        kind = 'splash'
        width = 128
        height = 128
    }
    icon {
        href = 'shortcut-32@2x.png'
        kind = 'selected'
        width = 16
        height = 16
        scale = 1
    }

    bundleArguments {
        licenseFile = 'LICENSE' // click through license file for some bundlers
        userJvmOptions = ['-Xmx': '128m', '-Xms': '128m'] // A mechanism to allow programs to adjust JVM Options
        preferencesID = 'com.shemnon.fullyExpressed' // implementation detail for user JVM options
    }
}

compileJava {
    sourceCompatibility = 7
    targetCompatibility = 7
}

jfxDeploy {
    // verbose output of the deployment, to the info chanel
    // it leaves files in the temp dir if set to true
    verbose = false
}

generateDebugKey {
    dname = 'ou=them'
    validity = 1
}

jar {
    manifest {
        attributes('-x-bogus': 'a bogus attribute',
                '-x-bogus-2': 'a second bogus attribute')
    }
}
